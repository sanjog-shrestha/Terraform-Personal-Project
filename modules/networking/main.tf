###############################################################################
# modules/networking/main.tf
###############################################################################

# ---VPC----------------------------------------------
resource "aws_vpc" "this" {
  cidr_block = var.vpc_cidr
  enable_dns_support = true
  enable_dns_hostnames = true

  tags = {
    Name = "${var.project_name}-${var.environment}-vpc"
  }
}

# ---Internet----------------------------------------------
resource "aws_internet_gateway" "this" {
  vpc_id = aws_vpc.this.id 

  tags = {
    Name = "${var.project_name}-${var.environment}-igw"
  }
}

# ---Public Subnets----------------------------------------------
resource "aws_subnet" "public" {
  count = length(var.public_subnet_cidrs)
  vpc_id = aws_vpc.this.id 
  cidr_block = var.public_subnet_cidrs[count.index]
  availability_zone =  var.availability_zones[count.index]
  map_public_ip_on_launch = true 

  tags = {
    Name = "${var.project_name}-${var.environment}-public-${count.index + 1}"
    Tier = "public"
  }
}

# ---Private Subnets----------------------------------------------
resource "aws_subnet" "private" {
  count = length(var.private_subnet_cidrs)
  vpc_id = aws_vpc.this.id 
  cidr_block = var.private_subnet_cidrs[count.index]
  availability_zone =  var.availability_zones[count.index]

  tags = {
    Name = "${var.project_name}-${var.environment}-public-${count.index + 1}"
    Tier = "private"
  }
}

# ---Elastic IP for NAT Gateway----------------------------------------------
resource "aws_eip" "nat" {
    count = var.enable_nat_gateway ? 1 : 0
    domain = "vpc"

    tags = {
        Name = "${var.project_name}-${var.environment}-nat-eip"
    }

    depends_on = [ aws_internet_gateway.this ]
}

# ---NAT Gateway (placed in first public subnet)----------------------------------------------
resource "aws_nat_gateway" "this" {
  count = var.enable_nat_gateway ? 1 : 0
  allocation_id = aws_eip.nat[0].id 
  subnet_id = aws_subnet.public[0].id

  tags = {
    Name = "${var.project_name}-${var.environment}-nat"
  }

  depends_on = [ aws_internet_gateway.this ]
}

# ---Public Route Table----------------------------------------------
resource "aws_route_table" "public" {
  vpc_id = aws_vpc.this.id 

  route {
    cidr_block = "0.0.0.0/0"
    gateway_id = aws_internet_gateway.this.id 
  }
  
  tags = {
    Name = "${var.project_name}-${var.environment}-public-rt"
  }

}

resource "aws_route_table_association" "public" {
  count = length(aws_subnet.public)
  subnet_id = aws_subnet.public[count.index].id 
  route_table_id = aws_route_table.public.id 
}

# ---Private Route Table----------------------------------------------
resource "aws_route_table" "private" {
  vpc_id = aws_vpc.this.id 

  dynamic "route" {
    for_each = var.enable_nat_gateway ? [1] : []
    content {
      cidr_block = "0.0.0.0/0"
      nat_gateway_id = aws_nat_gateway.this[0].id
    }
  }
  
  tags = {
    Name = "${var.project_name}-${var.environment}-private-rt"
  }

}

resource "aws_route_table_association" "private" {
  count = length(aws_subnet.private)
  subnet_id = aws_subnet.private[count.index].id 
  route_table_id = aws_route_table.private.id  
}